plugins {
    alias(libs.plugins.android.library)
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'com.ftcteams.behaviortrees'
    compileSdk 34

    defaultConfig {
        minSdk 24
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = project.properties["GROUP"]
            artifactId = project.properties["POM_ARTIFACT_ID"]
            version = project.properties["VERSION_NAME"]

            afterEvaluate {
                from components.release
            }
            pom {
                name = project.properties["POM_NAME"]
                description = project.properties["POM_DESCRIPTION"]
                url = project.properties["POM_URL"]
                licenses {
                    license {
                        name = project.properties["POM_LICENSE_NAME"]
                        url = project.properties["POM_LICENSE_URL"]
                    }
                }
                developers {
                    developer {
                        id = project.properties["POM_DEVELOPER_ID"]
                        name = project.properties["Alan Smith"]
                    }
                }
                scm {
                    connection = project.properties["POM_SCM_CONNECTION"]
                    url = project.properties["POM_SCM_URL"]
                }
            }
        }

    }
    repositories {
        maven {
            name = 'myRepo'
            url = layout.buildDirectory.dir("repo")
        }
    }
}
signing {
    sign publishing.publications
}